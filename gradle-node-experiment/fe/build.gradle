buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:0.11'
    }
}

apply plugin: 'java'
apply plugin: 'com.moowork.node'

sourceSets {
    main {
        resources {
            srcDir 'dist'
        }
    }
}

sourceCompatibility = 1.5
version = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

node {
    version = '4.1.1'
    download = true
    workDir = file("${project.projectDir}/nodejs")
}

task bowerInstall(type: NodeTask, dependsOn: 'npmInstall') {
    script = file('node_modules/bower/bin/bower')
    args = ['install']
}

task gruntBuild(type: NodeTask, dependsOn: 'bowerInstall') {
    script = file('node_modules/grunt-cli/bin/grunt')
    args = ['build']
}

task gruntClean(type: NodeTask, dependsOn: 'npmInstall') {
    script = file('node_modules/grunt-cli/bin/grunt')
    args = ['clean']
}

task gruntTest(type: NodeTask, dependsOn: 'bowerInstall') {
    script = file('node_modules/grunt-cli/bin/grunt')
    args = ['test']
}

processResources.dependsOn('gruntBuild')
clean.dependsOn('gruntClean')
test.dependsOn('gruntTest')
